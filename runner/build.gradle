buildscript {
   repositories {
      maven {
         url 'http://maven.aliyun.com/nexus/content/groups/public'
      }

      jcenter()
      mavenCentral()
   }
}

plugins {
   id 'org.springframework.boot' version '2.1.6.RELEASE'
   id 'io.spring.dependency-management' version '1.0.9.RELEASE'
   id 'java'
}

sourceCompatibility = '1.8'

configurations {
   api
   tools
   developmentOnly
   compile.extendsFrom(api)
   compileOnly.extendsFrom(tools, annotationProcessor)
}

repositories {
   maven {
      url 'http://maven.aliyun.com/nexus/content/groups/public'
   }

   jcenter()
   mavenCentral()
}

dependencies {
   // Spring
   implementation 'org.springframework.boot:spring-boot-starter-web'
   // ORM
   implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
   // Java Mail
   implementation 'org.springframework.boot:spring-boot-starter-mail'
   // AMQP
   implementation 'org.springframework.boot:spring-boot-starter-amqp'
   // WebSocket
   implementation 'org.springframework.boot:spring-boot-starter-websocket'

   // template
   implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
   implementation "ognl:ognl:${ognlVersion}"

   // Cache
   implementation 'org.springframework.boot:spring-boot-starter-cache'
   implementation 'net.sf.ehcache:ehcache'
   implementation 'org.springframework.boot:spring-boot-starter-data-redis'

   // datasource
   runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
   runtimeOnly 'com.h2database:h2' // for test and develop mode
   implementation "com.alibaba:druid:${druidVersion}"

   // security
   implementation("org.apache.shiro:shiro-ehcache:${shiroVersion}") {
      exclude(group: 'net.sf.ehcache')
   }
   implementation "org.apache.shiro:shiro-spring:${shiroVersion}"
   implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:${jasyptVersion}"

   // poi
   implementation "org.apache.poi:poi:${poiVersion}"
   implementation "org.apache.poi:poi-ooxml-schemas:${poiVersion}"
   implementation "org.apache.poi:poi-ooxml:${poiVersion}"

   // tools
   implementation "org.apache.commons:commons-lang3"
//   implementation 'org.springframework.boot:spring-boot-devtools'
   annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

   // springfox swagger
   implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
   implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

   // webjars
   implementation "org.webjars:jquery:${jqueryVersion}"
   implementation "org.webjars:bootstrap:${bootstrapVersion}"

   // test
   testImplementation('org.springframework.boot:spring-boot-starter-test') {
      exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
      exclude group: 'org.junit.platform', module: 'junit-platform-engine'
      exclude group: 'org.junit.platform', module: 'junit-platform-commons'
   }

   testImplementation("org.junit.jupiter:junit-jupiter-api:${jupiterVersion}") {
      exclude group: 'org.junit.platform', module: 'junit-platform-commons'
   }
   testImplementation("org.junit.jupiter:junit-jupiter-params:${jupiterVersion}") {
      exclude group: 'org.junit.platform', module: 'junit-platform-commons'
   }

   testImplementation("org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}") {
      exclude group: 'org.junit.platform', module: 'junit-platform-engine'
      exclude group: 'org.junit.platform', module: 'junit-platform-commons'
   }
   testImplementation "org.junit.platform:junit-platform-engine:1.6.2"
   testImplementation "org.junit.platform:junit-platform-commons:1.6.2"
}

test {
   useJUnitPlatform()

   testLogging {
      events "passed", "skipped", "failed"
   }
}

processResources {
   onlyIf {
      !file("$buildDir/resources/main/application.yml").exists()
   }

//   exclude {
//      "*.sql"
//   }

//   filesMatching('application.yml') {
//      String replacement = rootProject.project("${webModule}").projectDir.getCanonicalPath().replace('\\', '/')
//      replacement += '/dist'
//      filter ReplaceTokens, tokens: ['WEB_DIST_DIR': replacement]
//   }
}

task cleanAll(type: Delete, dependsOn: [clean]) {
   group 'build'
   delete = ['out']
}

task server() {
   group 'application'
   description 'run the application'

   doLast {
      javaexec {
         classpath sourceSets.main.runtimeClasspath
         def envMode = productionBuild ? 'prod' : 'dev'
         def debugCmd = productionBuild
            ? ''
            : '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'

         main = 'club.javafamily.runner.RunnerApplication'
         jvmArgs = [
            "-Dspring.profiles.active=${envMode}",
            '-Djava.rmi.server.hostname=127.0.0.1',
            debugCmd]
         args = ['--stop-port', '8082', '--stop-key', 'stop']
      }
   }
}

tasks.bootJar.configure {
   archivesBaseName = "javafamily-oa"
}

tasks.bootJar.dependsOn(tasks.findByPath(":${webModule}:build"))
